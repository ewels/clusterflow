#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$FindBin::Bin/../source";
use CF::Constants;
use CF::Helpers;

##########################################################################
# Copyright 2014, Philip Ewels (phil.ewels@babraham.ac.uk)               #
#                                                                        #
# This file is part of Cluster Flow.                                     #
#                                                                        #
# Cluster Flow is free software: you can redistribute it and/or modify   #
# it under the terms of the GNU General Public License as published by   #
# the Free Software Foundation, either version 3 of the License, or      #
# (at your option) any later version.                                    #
#                                                                        #
# Cluster Flow is distributed in the hope that it will be useful,        #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU General Public License      #
# along with Cluster Flow.  If not, see <http://www.gnu.org/licenses/>.  #
##########################################################################

# Get Options
my $required_cores;
my $required_mem;
my $required_modules;
my $help;
my $result = GetOptions ("cores=i" => \$required_cores, "mem=s" => \$required_mem, "modules" => \$required_modules, "help" => \$help);

# QSUB SETUP
# --cores i = offered cores. Return number of required cores.
if($required_cores){
	print 1;
	exit;
}
# --mem. Return the required memory allocation.
if($required_mem){
	print '500M';
	exit;
}
# --modules. Return csv names of any modules which should be loaded.
if($required_modules){
	print 'sratoolkit';
	exit;
}
# --help. Print help.
if($help){
	print "".("-"x23)."\n SRA FastQ Dump Module\n".("-"x23)."\n
This module uses the sra toolkit fastq-dump package
to extract .fastq files from .sra input. It gzips the fastq
files once produced.\n\n";
	exit;
}

# MODULE
# Read in the input files from the run file
my ($files, $runfile, $job_id, $prev_job_id, $cores, $mem, $parameters, $config_ref) = CF::Helpers::load_runfile_params(@ARGV);
my %config = %$config_ref;

open (RUN,'>>',$runfile) or die "Can't write to $runfile: $!";

# Go through each supplied file and run fastq-dump.
foreach my $file (@$files){

	my $fn_base = substr($file, 0, -4);
	my @outputfiles = ($fn_base."_1.fastq", $fn_base."_2.fastq");
	
	for (my $attempt = 1; $attempt < 6; $attempt++) {
		
		my $command = "fastq-dump --split-files ./$file";
		warn "\n###CFCMD $command\n\n";
		
		if(!system ($command)){
			warn "\n###CF FastQ Dump successfully exited on attempt $attempt\n";
			# FastQ Dump worked - print out resulting filenames
			foreach my $output_fn (@outputfiles){
				if(-e $output_fn){
					# Zip input files up again
					warn "gzipping dumped fastq file $output_fn..\n";
					if(!system("gzip  $output_fn")){
						$output_fn .= '.gz';
						print RUN "$job_id\t$output_fn\n";
					} else {
						warn "### Error - gzipping $output_fn exited with an error state. Printing ungzipped filename to run file. ###\n";
						print RUN "$job_id\t$output_fn\n";
					}
				} else {
					warn "\nSRA dump file $output_fn not found.. (Probably single end?)\n\n";
				}
			}
			last;
			
		} else {
			
			# FastQ Dump failed - clean up partially dumped files
			foreach my $output_fn (@outputfiles){
				if(-e $output_fn){
					unlink $output_fn or die "Could not delete $output_fn : $!";
				}
			}
			warn "###CF FastQ Dump failed on attempt $attempt: $? $!\n";
			
		}
	}
}

close (RUN);