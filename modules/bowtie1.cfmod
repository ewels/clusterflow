#!/usr/bin/env perl
use warnings;
use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$FindBin::Bin/../source";
use CF::Constants;
use CF::Helpers;

##########################################################################
# Copyright 2014, Philip Ewels (phil.ewels@babraham.ac.uk)               #
#                                                                        #
# This file is part of Cluster Flow.                                     #
#                                                                        #
# Cluster Flow is free software: you can redistribute it and/or modify   #
# it under the terms of the GNU General Public License as published by   #
# the Free Software Foundation, either version 3 of the License, or      #
# (at your option) any later version.                                    #
#                                                                        #
# Cluster Flow is distributed in the hope that it will be useful,        #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU General Public License      #
# along with Cluster Flow.  If not, see <http://www.gnu.org/licenses/>.  #
##########################################################################

# Get Options
my $required_cores;
my $required_mem;
my $required_modules;
my $run_fn;
my $version;
my $help;
my $result = GetOptions ("cores=i" => \$required_cores, "mem=s" => \$required_mem, "modules" => \$required_modules, "runfn" => \$run_fn, "version" => \$version, "help" => \$help);

# QSUB SETUP
# --cores i = offered cores. Return number of required cores.
if($required_cores){
    print CF::Helpers::allocate_cores($required_cores, 1, 8);
    exit;
}
# --mem. Return the required memory allocation.
if($required_mem){
    print CF::Helpers::allocate_memory($required_mem, '3G', '4G');
    exit;
}
# --modules. Return csv names of any modules which should be loaded.
if($required_modules){
    print 'bowtie,samtools';
    exit;
}
# --version. Returns version information about the module.
if($version){
    print "---------- Bowtie 1 version information ----------\n";
    print `bowtie --version`;
    print "\n------- End of Bowtie 1 version information ------\n";	
}
# --help. Print help.
if($help){
    print "".("-"x17)."\n Bowtie 1 Module\n".("-"x17)."\n
Bowtie is an ultrafast, memory-efficient short read aligner.
It aligns short DNA sequences (reads) to a reference genome. This script
works out the encoding of input files, guesses whether they're paired end
or not and runs bowtie. Output is piped through samtools to generate BAM files.

If input files are gzipped they will either be piped through zcat
(single end) or gunzipped, processed and zipped again (paired end).

To use this module to align microRNAs, use the parameter mirna, eg:
  #bowtie mirna

For further information about bowtie, please run bowtie --help\n\n";
    exit;
}

# MODULE
my $timestart = time;

# Read in the input files from the run file
my ($files, $runfile, $job_id, $prev_job_id, $cores, $mem, $parameters, $config_ref) = CF::Helpers::load_runfile_params(@ARGV);
my %config = %$config_ref;

# Check that we have a genome defined
if(!defined($config{bowtie_path})){
   die "\n\n### No bowtie path found in run file $runfile for job $job_id. Exiting.. ###";
} else {
    warn "\nAligning against ".$config{bowtie_path}."\n\n";
}

# Print the version if it's in the run file config
if(defined($config{verbose})){
    print "---------- Bowtie 1 version information ----------\n";
    print `bowtie --version`;
    print "\n------- End of Bowtie 1 version information ------\n";	
}

if(!defined($cores) || $cores <= 0){
    $cores = 1;
}

# See if we need to parse any parameters
my $mirna;
foreach my $parameter (@$parameters) {
	if ($parameter eq 'mirna') {
		$mirna = 1;
	}
}

open (RUN,'>>',$runfile) or die "Can't write to $runfile: $!";

# Separate file names into single end and paired end
my ($se_files, $pe_files) = CF::Helpers::is_paired_end(\%config, @$files);

# FastQ encoding type. Once found on one file will assume all others are the same
my $encoding = 0;


# Go through each single end files and run Bowtie
if($se_files && scalar(@$se_files) > 0){
	foreach my $file (@$se_files){
		
		# Figure out the encoding if we don't already know
		if(!$encoding){
			($encoding) = CF::Helpers::fastq_encoding_type($file);
		}
		my $enc = "";
		if($encoding eq 'phred33' || $encoding eq 'phred64' || $encoding eq 'solexa'){
			$enc = '--'.$encoding.'-quals';
		}
		
		my $output_fn = $file."_bowtie.bam";
		
		my $gzip = "";
		if($file =~ /\.gz$/){
			# $gzip = "gzip -dc $file | ";
			$gzip = "zcat $file | ";
			$file = "-";
		}
		
		my $command = "$gzip bowtie -p $cores -t -m 1 $enc --strata --best -S --chunkmbs 2048 ".$config{bowtie_path}." $file | samtools view -bS - > $output_fn";
		if ($mirna) {
		    $command = "$gzip bowtie -p $cores -t -n 0 -l 15 -e 99999 -k 200 $enc --best -S --chunkmbs 2048 ".$config{bowtie_path}." $file | samtools view -bS - > $output_fn"
		}
		warn "\n###CFCMD $command\n\n";
		
		if(!system ($command)){
			# Bowtie worked - print out resulting filenames
			my $duration =  CF::Helpers::parse_seconds(time - $timestart);
			warn "###CF Bowtie (SE mode) successfully exited, took $duration..\n";
			if(-e $output_fn){
				print RUN "$job_id\t$output_fn\n"; 
			} else {
				warn "\n###CF Error! Bowtie output file $output_fn not found..\n";
			}
		} else {
			warn "\n###CF Error! Bowtie (SE mode) exited in an error state for inpiut file '$file': $? $!\n\n";
		}
	}
}

# Go through the paired end files and run Bowtie
if($pe_files && scalar(@$pe_files) > 0){
	foreach my $files_ref (@$pe_files){
		my @files = @$files_ref;
		if(scalar(@files) == 2){
			
			# Figure out the encoding if we don't already know
			if(!$encoding){
				($encoding) = CF::Helpers::fastq_encoding_type($files[0]);
			}
			my $enc = "";
			if($encoding eq 'phred33' || $encoding eq 'phred64' || $encoding eq 'solexa'){
				$enc = '--'.$encoding.'-quals';
			}
			
			# Unzip gzipped input files
			if($files[0] =~ /.gz$/){
				warn "Unzipping input files..\n";
				system("gunzip ".$files[0]);
				system("gunzip ".$files[1]);
				$files[0] =~ s/.gz$//;
				$files[1] =~ s/.gz$//;
			}
			
			my $output_fn = $files[0]."_bowtie.bam";
			
			my $command = "bowtie -p $cores -t -m 1 $enc --strata --best --maxins 700 -S --chunkmbs 2048 ".$config{bowtie_path}." -1 ".$files[0]." -2 ".$files[1]." | samtools view -bSh 2>/dev/null - > $output_fn";

			if ($mirna) {
			    $command = "bowtie -p $cores -t -n 0 -l 15 -e 99999 -k 200 $enc --best --maxins 700 -S --chunkmbs 2048 ".$config{bowtie_path}." -1 ".$files[0]." -2 ".$files[1]." | samtools view -bSh 2>/dev/null - > $output_fn";
			}
			warn "\n###CFCMD $command\n\n";
			
			if(!system ($command)){
				# Bowtie worked - print out resulting filenames
				my $duration =  CF::Helpers::parse_seconds(time - $timestart);
				warn "###CF Bowtie (PE mode) successfully exited, took $duration..\n";
				if(-e $output_fn){
					print RUN "$job_id\t$output_fn\n";
				} else {
					warn "\n###CF Error! Bowtie output file $output_fn not found..\n";
				}
			} else {
				warn "\n###CF Error! Bowtie (PE mode) exited in an error state for input file '".$files[0]."': $? $!\n\n";
			}
			
			# Zip input files up again
			warn "Zipping input files..\n";
			system("gzip ".$files[0]);
			system("gzip ".$files[1]);
			
		} else {
			warn "\n###CF Error! Bowtie paired end files had ".scalar(@files)." input files instead of 2\n";
		}
	}
}


close (RUN);
